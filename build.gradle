buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorPluginVersion}")
    }
}

project.version = version

subprojects {
    group = 'org.conjugates'
    version = rootProject.version
}

project(":analyzer") {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'checkstyle'
    apply plugin: 'org.asciidoctor.convert'
    
    checkstyle {
       toolVersion = checkstyleToolVersion
       configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    }
    
    ext {
        snippetsDir = file('build/generated-snippets')
    }
    
    sourceCompatibility = 1.8
    
    repositories {
        mavenCentral()
    }
    
    dependencies {
        compile('org.springframework.boot:spring-boot-starter-aop')
        // compile('org.springframework.boot:spring-boot-starter-security')
        compile('org.springframework.boot:spring-boot-starter-web')

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    }
    
    test {
        outputs.dir snippetsDir
    }
    
    asciidoctor {
        attributes 'snippets': snippetsDir
        inputs.dir snippetsDir
        dependsOn test
    }
    
}
