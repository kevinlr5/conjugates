buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorPluginVersion}")
        classpath("com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}")
    }
}

apply from: 'git.gradle'

project.version = gitDescribe()

ext {
    gitCommit = gitCommitHash()
    dockerUsername = System.getenv("DOCKER_USERNAME")
}

subprojects {
    version = rootProject.version
}

def retry(times, sleepTimeMillis, body) {
  int retries = 0
  def exceptions = []
  while(retries++ < times) {
    try {
      return body.call()
    } catch(e) {
      exceptions << e
      println e.message
      sleep(sleepTimeMillis)
    }        
  }
  throw new RuntimeException("Failed after $times retries", exceptions)
}