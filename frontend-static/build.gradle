buildscript {
    repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
        classpath("com.moowork.gradle:gradle-node-plugin:${nodePluginVersion}")
    }
}

apply plugin: 'com.moowork.node'

buildDir = file('build')

node {
  version = nodeVersion
  download = true
}

task clean(type: Delete) {
  delete buildDir
}

task buildFrontend(type: NpmTask) {
  args = ['run', 'build']
  environment 'NODE_ENV': 'production'
}

task runFrontend(type: NpmTask) {
  args = ['start']
}

task shrinkwrap(type: NpmTask) {
  args = ['shrinkwrap']
}

task copyApiDocs(type: Copy, dependsOn: [':analyzer:asciidoctor', 'clean']) {
  from("${project(':analyzer').buildDir}/asciidoc/html5")
  into "${project.buildDir}/docs"
}


tasks.buildFrontend.dependsOn npmInstall, clean, copyApiDocs
tasks.runFrontend.dependsOn npmInstall, copyApiDocs
tasks.shrinkwrap.dependsOn buildFrontend
