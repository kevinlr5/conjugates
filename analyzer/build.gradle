buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorPluginVersion}")
    }
}

import com.bmuschko.gradle.docker.DockerRegistryCredentials
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerInspectContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer
import groovy.json.JsonSlurper

apply plugin: 'org.asciidoctor.convert'

ext {
    snippetsDir = file('build/generated-snippets')
    port = 9090
    containerName = 'analyzer'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("edu.stanford.nlp:stanford-corenlp:${stanfordNlpVersion}")
    compile("edu.stanford.nlp:stanford-corenlp:${stanfordNlpVersion}:models@jar")
    compile("com.google.guava:guava:${guavaVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

task prepareImageBuildDir(type: Copy) {
    from("${project.rootDir}/infrastructure/${containerName}") {
        filesMatching("application.properties") { props ->
            props.expand(version: project.version, commit: project.gitCommit)
        }
    }
    from("${project.buildDir}/libs") {
        include '*.jar'
        rename '.*jar', 'app.jar'
    }
    into(imageBuildDir)
}

task buildImage(type: DockerBuildImage) {
    dependsOn assemble, prepareImageBuildDir
    inputDir = imageBuildDir
    tag "${dockerUsername}/${containerName}:${project.version}"
}

task removeImage(type: DockerRemoveImage) {
    targetImageId { buildImage.getImageId() }
}

task pushImage(type: DockerPushImage) {
    dependsOn buildImage
    imageName "${dockerUsername}/${containerName}"
    tag project.version
    registryCredentials = new DockerRegistryCredentials()
    registryCredentials.username = dockerUsername
    registryCredentials.password = System.getenv("DOCKER_PASSWORD")
}

task createContainer(type: DockerCreateContainer) {
    dependsOn pushImage
    targetImageId { buildImage.getImageId() }
    portBindings = ["${port}:${port}"]
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId { createContainer.getContainerId() }
}

task stopContainer(type: DockerStopContainer) {
    targetContainerId { createContainer.getContainerId() }
}

task removeContainer(type: DockerRemoveContainer) {
    dependsOn stopContainer
    targetContainerId { createContainer.getContainerId() }
}

task testContainer() {
    dependsOn startContainer
    doLast {
        validateHttpEndpoint("http://localhost:${port}/api/info", containerName) { response ->
            def json = new JsonSlurper().parseText(response)
            def version = json['version']
            return version == project.version
        }
    }
    finalizedBy removeContainer, removeImage
}

test {
    dependsOn(":infrastructure:flywayMigrate")
    outputs.dir snippetsDir
    maxHeapSize = "1024m" // CIRCLE CI nodes have limited memory
    finalizedBy(":infrastructure:flywayClean")
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

tasks.check.dependsOn testContainer, asciidoctor
// CIRCLE CI cannot remove docker images/containers
[tasks.removeContainer, tasks.removeImage].each { t -> t.enabled = !System.getenv("CIRCLECI") }